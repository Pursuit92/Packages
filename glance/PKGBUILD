# Maintainer: Josh Chase <jcjoshuachase@gmail.com>

pkgname=glance
pkgver=2012.1.2
pkgrel=1
_ostackver=essex
pkgdesc="OpenStack image service"
arch=('any')
license=('custom')
url="https://launchpad.net/$pkgname"
depends=('python2'
	 'python2-httplib2'
	 'python2-webob'
	 'python2-eventlet'
	 'python2-greenlet'
	 'python-paste'
	 'python-paste-deploy'
	 'python-routes'
	 'python2-sqlalchemy'
	 'python2-migrate'
	 'python2-passlib'
	 'python2-lxml'
	 'python-anyjson'
	 'python-wsgiref'
	 'python2-argparse'
	 'python2-boto'
	 'python-xattr'
	 'python2-kombu'
	 'python2-crypto'
	 'python2-pysendfile'
	 'python2-iso8601')
makedepends=('python2-distribute')
source=("https://launchpad.net/$pkgname/$_ostackver/$pkgver/+download/$pkgname-$pkgver.tar.gz")
md5sums=('544496fde64a341a4f39e15f4b258093')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  find . -type f -exec sed -i -e 's|^#!/usr/bin/python$|#!/usr/bin/python2|' \
    -e 's|^#!/usr/bin/env python$|#!/usr/bin/env python2|' {} +
  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=1
  
  install -dm600 "$pkgdir"/etc/$pkgname
  install -Dm600 etc/* "$pkgdir"/etc/$pkgname/
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
