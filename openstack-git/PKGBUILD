# Maintainer: Josh Chase <jcjoshuachase@gmail.com>

pkgname=openstack-git
pkgver=essex
_components=('nova' 'glance' 'keystone' 'swift')
pkgrel=1
pkgdesc="OpenStack Cloud Platform"
arch=('i686' 'x86_64')
license=('apache')
url="https://launchpad.net/openstack"
depends=('python2'
         'bridge-utils'
         'libxslt'
         'ebtables'
         'libguestfs'
         'dnsmasq'
         'libxslt' )
conflicts=openstack
backup=( 'etc/nova/nova.conf'
         'etc/nova/api-paste.ini'
         'etc/glance/glance-api.conf'
         'etc/glance/glance-registry.conf'
         'etc/glance/glance-api-paste.ini'
         'etc/glance/glance-registry-paste.ini'
         'etc/keystone/keystone.conf' )
source=("glance-api.service"
        "glance-registry.service"
        "keystone.service"
        "nova-api.service"
        "nova-cert.service"
        "nova-compute.service"
        "nova-console.service"
        "nova-consoleauth.service"
        "nova-network.service"
        "nova-scheduler.service"
        "nova-volume.service")
md5sums=( 3750be6bb2d87bfe65e7f3f494fb4acb
          5c9506f59e36c805dcde85b2b3ff70c5
          f4bee2af83f7b7931ae325005a2729b8
          458528a5b7dca079b7d87ea5ec561a8c
          13ccd49547fadcc50f218c4973fd17ca
          96b5da3d49f31ac46e406e8f5469cfa8
          9110f0690e3dfc7a1a352999e54798eb
          c5f478bd701c5eeaacca49e5159cdd9c
          f78182485d9570c5e1dbb6442d35b30e
          a00cb806dcfec916cd717c009312b0de
          04cf5aadeb06ac61cb4dd82856d8cab4)
makedepends=('python2-virtualenv')

builddir=$srcdir/../build
venv=$builddir/opt/openstack

_prep_source() {

  pushd $srcdir
  for i in ${_components[*]}
  do
    if [ -d $i ]; then
      echo "Directory found, not cloning."
    else
      git clone https://github.com/openstack/$i $i \
        --depth=1 --branch stable/essex
    fi
  done

  find . -type f -exec sed -i -e 's|^#!/usr/bin/python$|#!/usr/bin/python2|' \
    -e 's|^#!/usr/bin/env python$|#!/usr/bin/env python2|' {} +

  popd
}

build() {
  _prep_source
  pushd $srcdir

  # Build the virtualenv and install dependencies
  mkdir -p $venv /tmp/piptemp $builddir/etc
  virtualenv2 $venv/

  for i in *
  do
    if [ -d $i ]; then
  
      svc=`echo $i | sed s/-.*//`

      [ -e $i/tools/pip-requires ] && $venv/bin/pip install --download-cache /tmp/piptemp -r $i/tools/pip-requires
    
      pushd $i
      $venv/bin/python setup.py build
      popd
    
  
    fi
  done

  $venv/bin/pip install python-keystoneclient

  popd
}

package() {
  for i in ${_components[*]}; do
    svc=$i
    pushd $srcdir/$svc*
    $venv/bin/python setup.py install --optimize=1
    if [ ! -d etc/$svc ] 
    then
      cp -Tr etc $builddir/etc/$svc
    else
      cp -r etc/$svc $builddir/etc/
    fi
    popd
  done

  virtualenv2 --relocatable $venv/
  
  mkdir -p $pkgdir/{usr/lib/systemd/system,var/lib/openstack/{images,instances},opt/}
  for i in $srcdir/*service; do
    install -Dm755 $i $pkgdir/usr/lib/systemd/system/
  done
  cp -Tr $venv $pkgdir/opt/openstack-essex
  cp -Tr $builddir/etc $pkgdir/etc
}
