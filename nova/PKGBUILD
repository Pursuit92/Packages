# Maintainer: Josh Chase <jcjoshuachase@gmail.com>

pkgname=nova
pkgver=2012.1.2
_clientname=python-novaclient
_clientver=2.8.0
pkgrel=1
pkgdesc="OpenStack cloud computing fabric controller"
arch=('any')
license=('custom')
url="https://launchpad.net/nova"
depends=('python2'
         'python2-cheetah'
         'python2-amqplib'
         'python2-carrot'
         'python2-lockfile'
         'python2-daemon'
         'python2-gflags'
         'python2-netaddr'
         'glance'
         'python2-suds'
         'python-paramiko'
         'python2-feedparser'
         'python-babel' )
makedepends=('python2-distribute')
source=("https://launchpad.net/nova/essex/2012.1.2/+download/nova-2012.1.2.tar.gz"
        "http://pypi.python.org/packages/source/${_clientname:0:1}/$_clientname/$_clientname-$_clientver.tar.gz")
md5sums=('e829334fc8ba90160c59801ff372d003'
         'ad748ff19ee7bcfd5a8daab4eca82321')

build() {
  cd $srcdir

  find . -type f -exec sed -i -e 's|^#!/usr/bin/python$|#!/usr/bin/python2|' \
    -e 's|^#!/usr/bin/env python$|#!/usr/bin/env python2|' {} +

  cd $srcdir/$_clientname-$_clientver
  python2 setup.py build

  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py build
}

package() {
  cd $srcdir/$_clientname-$_clientver
  python2 setup.py install --root=$pkgdir

  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=1
  
  install -dm600 "$pkgdir"/etc/$pkgname
  install -Dm600 etc/$pkgname/* "$pkgdir"/etc/$pkgname/
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
